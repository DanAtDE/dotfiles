# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-hg
epoch=1
pkgver=244
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://hg.suckless.org/st'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11')
makedepends=('mercurial' 'ncurses')
source=(config.def.h)
md5sums=('73b019d111bf90fa345f3d9b41a4b9ee')
provides=('st')
conflicts=('st')

_hgroot='http://hg.suckless.org/'
_hgrepo='st'

build() {
	cd "${srcdir}"
	msg "Connecting to Mercurial server...."

	if [[ -d "$_hgrepo" ]]; then
		cd "$_hgrepo"
	else
		hg clone -r "$pkgver" "${_hgroot}${_hgrepo}"
	fi

	rm -rf "$srcdir/$_hgrepo-build"
	cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
	cp $startdir/config.def.h "$srcdir/$_hgrepo-build/config.def.h"
	cd "$srcdir/$_hgrepo-build"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "$srcdir/$_hgrepo-build"
	make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
